plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'maven-publish'
}

apply from: "$rootDir/versions.gradle"
apply from: "$rootDir/dependencies.gradle"

android {
    compileSdkVersion version_compile_sdk
    buildToolsVersion version_build_tools

    defaultConfig {
        minSdkVersion version_min_sdk
        targetSdkVersion version_target_sdk
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation core.kotlinStdLib
    implementation core.coreKtx
    implementation core.appCompat
    implementation core.googleMaterial
    implementation core.constraintLayout

    implementation rxjava.rxJava
    implementation rxjava.rxJavaAndroid
    implementation rxjava.rxJavaBinding
    implementation rxjava.rxJavaBindingCore
    implementation rxjava.rxJavaBindingAppCompat
    implementation rxjava.rxJavaBindingMaterial

    implementation androidxadd.xactivity
    implementation androidxadd.xfragment

    testImplementation testing.junit
    androidTestImplementation testing.extJunit
    androidTestImplementation testing.espresso
}

/**
 * PUBLISHING SECTION
 */
def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def githubUser  = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
def githubKey   = githubProperties['gpr.publishkey'] ?: System.getenv("GPR_API_KEY")
def githubRepo  = githubProperties['gpr.repo'] ?: "android.library.rxutilities"

publishing {
    publications {
        release(MavenPublication) {
            groupId group_id
            version android.defaultConfig.versionName
            artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/$githubUser/$githubRepo")

            credentials {
                username = githubUser
                password = githubKey
            }
        }
    }
}